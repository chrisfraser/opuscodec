buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.github.chrisfraser'
def libVersion = '1.3.5'
def artifactName = 'opuscodec'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 4
        versionName libVersion
        setProperty("archivesBaseName", artifactName + "-" + libVersion)
        consumerProguardFiles 'proguard-rules.pro'
        externalNativeBuild {
            ndkBuild {
                // We don't build for arm64-v8a for now, because ASM optimizations do not compile
                // (at least for the default floating point implementation) and ARM64 supports
                // also armeabi-v7a binaries natively, so let's use those, which should be more
                // efficient.
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = artifactName
                version = libVersion
            }
        }
    }
}